apply plugin: 'com.android.application'

def dq(s) {
    if (s != null) {
        return "\"${s}\"".toString()
    }
    else {
        return "null"
    }
}

def stringProperty(variant, props, key) {
    variant.buildConfigField("String", key, dq(props[key]))
}

def propertiesFile(variantName) {
    Properties props = new Properties()
    File f = new File("constants.${variantName.toLowerCase()}.properties")
    if (f.exists()) {
        props.load(new FileInputStream(f))
    }
    return props
}

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "nom.bruno.tasksapp"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "pt_BR"
    }
    buildTypes {
        debug {
            debuggable true
        }
        deviceDebug {
            initWith debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all {variant ->
            def props = propertiesFile(variant.name)
            stringProperty(variant, props, "INTERNAL_SERVICE_URL")
            stringProperty(variant, props, "EXTERNAL_SERVICE_URL")
            stringProperty(variant, props, "SENTRY_DSN")
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3.2'
    testCompile 'org.awaitility:awaitility:3.0.0'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.9'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.getsentry.raven:raven-android:8.0.3'
}
